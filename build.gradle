// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.3.10"
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://maven.google.com' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath 'io.fabric.tools:gradle:1.26.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven { url 'https://maven.google.com' }
        google()
        jcenter()
    }

    ext.versionMajor = 1
    ext.versionMinor = 0
    ext.versionPatch = 1
    ext.versionClassifier = "SNAPSHOT"

    project.ext {
        versionCode = ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
        versionName = getVersionName()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

String getVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null && !ext.versionClassifier.isEmpty()) {
        versionName = versionName + "-" + ext.versionClassifier
    }
    return versionName;
}

subprojects {
    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support'
                    && !details.requested.name.contains('multidex') ) {
                details.useVersion "$rootProject.supportVersion"
            }
        }
    }
}

ext {
    minSdkVersion = 19
    targetSdkVersion = 27
    compileSdkVersion = 28
    buildToolsVersion = "28.0.3"
    supportVersion = "28.0.0"
}